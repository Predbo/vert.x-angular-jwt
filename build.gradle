plugins {
    id 'java'
    id 'application'
    id 'eclipse'    
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

version = '3.2.0'
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

dependencies {
	compile "io.vertx:vertx-core:$version"
	compile "io.vertx:vertx-web:$version"
}


def mainVerticle = "java:de.predbo.vertx.HelloWorldVerticle"

// Vert.x watches for file changes in all subdirectories of src/ but only for files with .java extension
def watchForChange = 'src/**/*'

// Vert.x will call this task on changes
def doOnChange = './gradlew classes'

run {
  args = ['run', mainVerticle, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': mainVerticle
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
